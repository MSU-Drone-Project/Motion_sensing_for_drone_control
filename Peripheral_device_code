//Peripheral device
#include <ArduinoBLE.h>

BLEService droneService("19B10000-E8F2-537E-4F6C-D104768A1214"); // BLE drone Service

// BLE LED Switch Characteristic - custom 128-bit UUID, read and writable by central
BLEByteCharacteristic ascendCharacteristic("19B10001-E8F2-537E-4F6C-D104768A1214", BLERead | BLEWrite);
BLEByteCharacteristic descendCharacteristic("19B10002-E8F2-537E-4F6C-D104768A1214", BLERead | BLEWrite);
BLEByteCharacteristic gestureCharacteristic("19B10003-E8F2-537E-4F6C-D104768A1214", BLERead | BLEWrite);

// variables for button                      
const int ascendLED = 3;                         
const int descendLED = 4;                                              
const int gestureLED = 6;  

void setup() {
  Serial.begin(9600);
  while (!Serial);

  // use the LED as an output
  pinMode(ascendLED, OUTPUT); 
  pinMode(descendLED, OUTPUT); 
  pinMode(gestureLED, OUTPUT); 

    // begin initialization
  if (!BLE.begin()) {
    Serial.println("starting BLE failed!");

    while (1);
  }

    // set the local name peripheral advertises
  BLE.setLocalName("BLE Peripheral - Drone");
  
  // set the UUID for the service this peripheral advertises:
  BLE.setAdvertisedService(droneService);

  // add the characteristics to the service
  droneService.addCharacteristic(ascendCharacteristic);
  droneService.addCharacteristic(descendCharacteristic);
  droneService.addCharacteristic(gestureCharacteristic);

  // add the service
  BLE.addService(droneService);

  // set the initial value for the characeristic
  ascendCharacteristic.writeValue(0);
  descendCharacteristic.writeValue(0);
  gestureCharacteristic.writeValue(0);

  // start advertising
  BLE.advertise();

  Serial.println("Drone peripheral device waiting for connection");
}

void loop() {
  // listen for BLE peripherals to connect:
  BLEDevice central = BLE.central();

  // if a central is connected to peripheral:
  if (central) {
    Serial.println("Connected to BLE Central - Remote control");

    // while the central is still connected to peripheral:
    while (central.connected()) {

      // device writes to the characteristic, lighting LEDs
      if (ascendCharacteristic.written()) {
        if (ascendCharacteristic.value()) {
          Serial.println("Drone - has ascended");
          digitalWrite(ascendLED, HIGH);         // will turn the LED on
        } // end value() 
        else {                                   
          digitalWrite(ascendLED, LOW);          // will turn the LED off
        } // end else
      }// end written
            // device writes to the characteristic, lighting LEDs
      if (descendCharacteristic.written()) {
        if (descendCharacteristic.value()) {
          Serial.println("Drone - has landed");
          digitalWrite(descendLED, HIGH);         // will turn the LED on
        } // end value() 
        else {                                    
          digitalWrite(descendLED, LOW);          // will turn the LED off
        } // end else
      }// end written
      if (gestureCharacteristic.written()) {
        if (gestureCharacteristic.value()) {
          Serial.println("Drone - ready to perform gesture");
          digitalWrite(gestureLED, HIGH);         // will turn the LED on
          delay(5000);
          digitalWrite(gestureLED, LOW);          // will turn the LED off
          Serial.println("Drone - has finished performing gesture and is currently ascended");
        } // end value() 
        else {                                    
          //Serial.println("Drone - has ascended 2");
          digitalWrite(gestureLED, LOW);          // will turn the LED off
        } // end else
      }// end written
    }// end central.connected

    // when the central disconnects, print it out:
    Serial.print("Disconnected from central");
  }
}
